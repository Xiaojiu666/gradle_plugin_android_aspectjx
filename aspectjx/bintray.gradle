apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = "com.hujiang.aspectjx"
version = "2.0.10"
project.archivesBaseName = "gradle-android-plugin-aspectjx"

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

ext {
    pomFilePath = "${project.buildDir.absolutePath}/tmp/pom.xml"
    pomFile = file(pomFilePath)
}

configurations {
    pom
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
    if (pomFile.exists()) {
        pom pomFile
    }
}

task signJars(type: Sign, dependsOn: [jar, javadocJar, sourceJar]) {
    sign configurations.archives
}

task signPom(type: Sign) {
    sign configurations.pom
}

if (project.ext.pomFile.exists()) {
    task preparePublication(dependsOn: [signJars, signPom])
} else {
    task preparePublication(dependsOn: signJars)
}

def getSignatureFiles = {
    def allFiles = project.tasks.signJars.signatureFiles.collect{it}
    def signedSources = allFiles.find{ it.name.contains('-sources') }
    def signedJavadoc = allFiles.find{ it.name.contains('-javadoc') }
    def signedJar = (allFiles - [signedSources, signJars])[0]
    return [
            [archive: signedSources, classifier: 'sources', extension: 'jar.asc'],
            [archive: signedJavadoc, classifier: 'javadoc', extension: 'jar.asc'],
            [archive: signedJar, classifier: 'null', extension: 'jar.asc']
    ]
}

def getPomSignature = {
    return project.tasks.signPom.signatureFiles.collect{ it }[0]
}


install {
    repositories.mavenInstaller {
// generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                name "gradle-android-plugin-aspectjx" // #CONFIG# // project title
                url "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx"

                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution "repo"
                    }
                }
                developers {
                    developer {
                        id  "firefly1126"
                        name "firefly1126"
                        email "xiaoming1109@gmail.com"
                    }
                }

                organization {
                    name "hujiang, Inc."
                    url "http://www.hujiang.com"
                }

                scm {
                    connection "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx.git"
                    developerConnection "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx/issues"
                    url "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx"
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayAPIKey
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "gradle-android-plugin-aspectjx"
        desc = "A Gradle plugin which enables AspectJ for Android builds. supports Kotlin, aar, jar aspect"
        websiteUrl = "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx"
        vcsUrl = "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx.git"
        issueTrackerUrl = "https://github.com/HujiangTechnology/gradle_plugin_android_aspectjx/issues"
        licenses = ["Apache-2.0"]
        labels = 'Groovy'
        publicDownloadNumbers = true
        publish = true
    }
}